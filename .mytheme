# Enable command substitution in prompt
setopt PROMPT_SUBST

# Color definitions
# Check terminal color support
if [[ "$COLORTERM" == "truecolor" ]] || [[ "$TERM" == *"24bit"* ]]; then
    # True color (24-bit) support
    PRINCESS_WHITE='#FFFFFF'
    PRINCESS_TAN='#CC3802'
    PRINCESS_TEAL='#047E84'
    PRINCESS_PLUM='#9A348E'
    PRINCESS_BLUSH='#DA627D'
    PRINCESS_SALMON='#FCA17D'
    PRINCESS_SKY='#86BBD8'
    PRINCESS_TEAL_BLUE='#33658A'
else
    # Fallback to 256 colors
    PRINCESS_WHITE='255'    # Pure white
    PRINCESS_TAN='166'     # Dark orange
    PRINCESS_TEAL='30'     # Deep teal
    PRINCESS_PLUM='161'    # Medium purple
    PRINCESS_BLUSH='161'   # Pink
    PRINCESS_SALMON='216'  # Light salmon
    PRINCESS_SKY='110'     # Light blue
    PRINCESS_TEAL_BLUE='25' # Deep blue
fi

# Define special characters with proper escaping
POWERLINE_SYMBOL=$'\ue0b0'
HEART_SYMBOL=$'\u2764'
ARROW_SYMBOL=$'\u279c'
STASH_SYMBOL=$'\ueb4b'
NODE_SYMBOL=$'\ue718'
LEADING_ROUNDED=$'\ue0b6'
ENDING_ROUNDED=$'\ue0b4'
FOLDER_SYMBOL=$'\uF115'
GIT_SYMBOL=$'\ue725'

# Git segment function
princess_git_info() {
    local ref
    ref=$(command git symbolic-ref HEAD 2> /dev/null) || \
    ref=$(command git rev-parse --short HEAD 2> /dev/null) || return 0
    echo " ${ARROW_SYMBOL} (${ref#refs/heads/}$(git_stash_count)) ${GIT_SYMBOL}"
}

git_stash_count() {
    local stash_count
    stash_count=$(git stash list 2>/dev/null | wc -l | tr -d ' ')
    [[ "$stash_count" -gt 0 ]] && echo " ${STASH_SYMBOL} ${stash_count}"
}

# Node segment function
princess_node_info() {
    # First check if node exists
    if ! command -v node &> /dev/null; then
        echo "node not available"
    fi
    
    # Then check for git repo or package.json
    if git rev-parse --is-inside-work-tree &> /dev/null || [ -f "package.json" ]; then
        echo "${NODE_SYMBOL} $(node -v)"
    else
        echo "local ${FOLDER_SYMBOL}"
    fi
}

# Time segment function
princess_time_info() {
    echo " ${HEART_SYMBOL} $(date '+%H:%M') "
}

princess_path_info() {
    echo " ${FOLDER_SYMBOL} %~ "
}

# Add new function to check if in git repo
is_git_repo() {
    git rev-parse --is-inside-work-tree &> /dev/null
}

# Conditional git segment
princess_git_segment() {
    if is_git_repo; then
        echo "%{%K{${PRINCESS_SALMON}}%F{${PRINCESS_BLUSH}}%}${POWERLINE_SYMBOL}%{%F{${PRINCESS_WHITE}}%}$(princess_git_info) %{%K{${PRINCESS_SKY}}%F{${PRINCESS_SALMON}}%}"
    else
        echo "%{%K{${PRINCESS_SKY}}%F{${PRINCESS_BLUSH}}%}"
    fi
}

# Update prompt definitions
PROMPT='%{%k%F{${PRINCESS_PLUM}}%}${LEADING_ROUNDED}%{%K{${PRINCESS_PLUM}}%F{${PRINCESS_WHITE}}%}%n %{%K{${PRINCESS_BLUSH}}%F{${PRINCESS_PLUM}}%}${POWERLINE_SYMBOL}'
PROMPT+='%{%K{${PRINCESS_BLUSH}}%F{${PRINCESS_WHITE}}%} %~ '
PROMPT+='$(princess_git_segment)${POWERLINE_SYMBOL}'
PROMPT+='%{%K{${PRINCESS_SKY}}%F{${PRINCESS_WHITE}}%} $(princess_node_info) %{%K{${PRINCESS_TEAL_BLUE}}%F{${PRINCESS_SKY}}%}${POWERLINE_SYMBOL}'
PROMPT+='%{%K{${PRINCESS_TEAL_BLUE}}%F{${PRINCESS_WHITE}}%}$(princess_time_info)%{%k%F{${PRINCESS_TEAL_BLUE}}%}${ENDING_ROUNDED}% '
PROMPT+=$'\n %{%{%k%F{${PRINCESS_PLUM}}%}└➤ %{%f%}'